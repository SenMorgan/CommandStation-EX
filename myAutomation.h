/* ########### NOTES ############
- To test PA0 input of expander, send this command to Serial port:
    <S 3 179 1>
    Then it must return messages depending on the state of the input:
    00:50:22.425 > <Q 2>
    00:50:23.145 > <q 2>
- Default volume by CV:
  63 - 64 or (main volume: 64 - 100%, 20 - 31%)
  121 - 64 (low horn)
  122 - 64
  123 - 64 (engine sound)

- Nano Servo max angles:
  <D SERVO 100 60 2>
  <D SERVO 100 560 2>

  // Test L_EDGE and SIGNAL_1
  SEQUENCE(201)
    GREEN(SIGNAL_1)
    AT(L_EDGE)
      AMBER(SIGNAL_1)
      DELAY(500)
      RED(SIGNAL_1)
    AT(-L_EDGE)
      AMBER(SIGNAL_1)
      DELAY(500)
      GREEN(SIGNAL_1)
    FOLLOW(201)


  // Test signals
  SEQUENCE(202)
    RED(SIGNAL_1)
    DELAY(500)
    AMBER(SIGNAL_1)
    DELAY(500)
    GREEN(SIGNAL_1)
    DELAY(500)
    FOLLOW(202)
*/

// ##################################
// ########### CONFIGURATION #########
// ##################################

// ########### STARTUP ###########
AUTOSTART
  SET_TRACK(A, MAIN)
  SET_TRACK(B, MAIN)
  POWERON

DONE

// ########### LOCOMOTIVES ###########
// ROSTER(999,"Loco Name","F0/F1/*F2/F3/F4/F5/F6/F7/F8")

// Vossloh 1701
ROSTER(4,"Vossloh 1701","Lights/Engine Sound/*Horn LO/*Horn HI/Coupler/Ventilator/Shunting Mode/Compressor///Sanding///*Compressed Air/Brake/Cabin Light")
ALIAS(VOSSLOH, 4)
ALIAS(VOSSLOH_LIGHTS_F, 0)
ALIAS(VOSSLOH_ENGINE_SOUND_F, 1)
ALIAS(VOSSLOH_HORN_LO_F, 2)
ALIAS(VOSSLOH_HORN_HI_F, 3)
ALIAS(VOSSLOH_VENTILATOR_F, 5)
ALIAS(VOSSLOH_SHUNTING_MODE_F, 6)
ALIAS(VOSSLOH_COMPRESSOR_F, 7)
ALIAS(VOSSLOH_HORN_LO_SHORT_F, 11)
ALIAS(VOSSLOH_HORN_HI_SHORT_F, 12)
ALIAS(VOSSLOH_BRAKE_F, 14)
ALIAS(VOSSLOH_INTERIOR_LIGHT_F, 15)

// SBB Cargo Re 482
ROSTER(5,"SBB Cargo Re 482","Lights/Power Sound/*Multiple Horns/*Multiple Horns Fancy/*Brake Squeal/Engine Mode/Detonators/Cabin Start-Up/Doors//Air Con/Wipers/Rail Clack/*Pantograph Arc/Flange Squeal//Coupling/*Safety Announcements/*Destination Announcements/*Guards Whistle/Station Sound/Inspection Lights/Cabin Light///Shunting Mode/*Creep Mode/*Dynamic Brake/AFC Enable")
ALIAS(CARGO, 5)
ALIAS(CARGO_LIGHTS_F, 0)
ALIAS(CARGO_POWER_SOUND_F, 1)
ALIAS(CARGO_MULTIPLE_HORNS_F, 2)
ALIAS(CARGO_MULTIPLE_HORNS_FANCY_F, 3)
ALIAS(CARGO_BRAKE_F, 4)
ALIAS(CARGO_ENGINE_MODE_F, 5)
ALIAS(CARGO_DETONATORS_F, 6)
ALIAS(CARGO_CAB_START_UP_F, 7)
ALIAS(CARGO_DOORS_F, 8)
ALIAS(CARGO_AIR_CONDITIONING_F, 10)
ALIAS(CARGO_WIPERS_F, 11)
ALIAS(CARGO_RAIL_CLACK_F, 12)
ALIAS(CARGO_PANTOGRAPH_ARC_F, 13)
ALIAS(CARGO_FLANGE_SQUEAL_F, 14)
ALIAS(CARGO_COUPLING_F, 16)
ALIAS(CARGO_SAFETY_ANNOUNCEMENTS_F, 17)
ALIAS(CARGO_DESTINATION_ANNOUNCEMENTS_F, 18)
ALIAS(CARGO_GUARDS_WHISTLE_F, 19)
ALIAS(CARGO_STATION_SOUND_F, 20)
ALIAS(CARGO_INSPECTION_LIGHTS_F, 21)
ALIAS(CARGO_CABIN_LIGHT_F, 22)
ALIAS(CARGO_SHUNTING_MODE_F, 25)
ALIAS(CARGO_CREEP_MODE_F, 26)
ALIAS(CARGO_DYNAMIC_BRAKE_F, 27)
ALIAS(CARGO_AFC_ENABLE_F, 28)

// ########### SENSORS ###########
ALIAS(R_EDGE, 164)  // Right side - edge
ALIAS(R_WALL, 165)  // Right side - wall
ALIAS(L_WALL, 178)  // Left side - wall
ALIAS(L_EDGE, 179)  // Left side - edge

// ########### SIGNALS ###########
SIGNAL(100, 101, 102)
ALIAS(SIGNAL_1, 100)

// ########### SERVOS ###########
ALIAS(SERVO1, 103)

// ########### TURNOUTS ###########
// TURNOUT( id, addr, sub_addr [, "description"] )
// Take the address and divide by 4. The result is the address, the remainder is the subaddress.
// For example, Address 51 (no subaddress) is DCC++ Address 12, Subaddress 3.
ALIAS(TURNOUT_WALL, 101)
TURNOUT(TURNOUT_WALL, 1, 0, "Near wall turnout")
ALIAS(TURNOUT_EDGE, 102)
TURNOUT(TURNOUT_EDGE, 1, 1, "Near edge turnout")



// ##################################
// ########### SEQUENCES ############
// ##################################

// Test sequence for the servo
ALIAS(SERVO_TEST)
SEQUENCE(SERVO_TEST)
  SERVO2(SERVO1, 60, 6000)
  DELAY(6000)
  SERVO2(SERVO1, 560, 6000)
  RETURN

AUTOMATION(302,"Pos 60 - 10000")
  SERVO2(SERVO1, 60, 10000)
  DONE
AUTOMATION(303,"Pos 560 - 11000")
  SERVO2(SERVO1, 560, 11000)
  DONE
AUTOMATION(304,"Pos 60 - 12000")
  SERVO2(SERVO1, 60, 12000)
  DONE
AUTOMATION(305,"Pos 560 - 13000")
  SERVO2(SERVO1, 560, 13000)
  DONE
AUTOMATION(306,"Pos 60 - 14000")
  SERVO2(SERVO1, 60, 14000)
  DONE
AUTOMATION(307,"Pos 560 - 15000")
  SERVO2(SERVO1, 560, 15000)
  DONE



// ########### TURNOUT SEQUENCES ###########
// Mirror the state of TURNOUT_WALL to TURNOUT_EDGE.
// Send command a few times to the both turnouts to
// ensure they are in the correct state.
ONTHROW(TURNOUT_WALL)
  DELAY(500) THROW(TURNOUT_WALL) DELAY(500)
  THROW(TURNOUT_EDGE) DELAY(500)
  THROW(TURNOUT_EDGE) DELAY(500)
  THROW(TURNOUT_EDGE)
DONE
ONCLOSE(TURNOUT_WALL)
  DELAY(500) CLOSE(TURNOUT_WALL) DELAY(500)
  CLOSE(TURNOUT_EDGE) DELAY(500)
  CLOSE(TURNOUT_EDGE) DELAY(500)
  CLOSE(TURNOUT_EDGE)
DONE

// ########### HORN SEQUENCES ###########
ALIAS(HORN_VOSSLOH_HI_SHORT)
SEQUENCE(HORN_VOSSLOH_HI_SHORT)
  FON(VOSSLOH_HORN_HI_SHORT_F)  DELAY(1000)  FOFF(VOSSLOH_HORN_HI_SHORT_F)
  RETURN
ALIAS(HORN_VOSSLOH_HI_LONG)
SEQUENCE(HORN_VOSSLOH_HI_LONG)
  FON(VOSSLOH_HORN_HI_F)  DELAY(1500)  FOFF(VOSSLOH_HORN_HI_F)
  RETURN
ALIAS(HORN_VOSSLOH_LOW_SHORT)
SEQUENCE(HORN_VOSSLOH_LOW_SHORT)
  FON(VOSSLOH_HORN_LO_SHORT_F)  DELAY(1000)  FOFF(VOSSLOH_HORN_LO_SHORT_F)
  RETURN
ALIAS(HORN_VOSSLOH_LOW_LONG)
SEQUENCE(HORN_VOSSLOH_LOW_LONG)
  FON(VOSSLOH_HORN_LO_F)  DELAY(1500)  FOFF(VOSSLOH_HORN_LO_F)
  RETURN
ALIAS(HORN_VOSSLOH_LOW_LONG_AFTER_DELAY)
SEQUENCE(HORN_VOSSLOH_LOW_LONG_AFTER_DELAY)
  DELAY(11000)
  XFON(VOSSLOH, VOSSLOH_HORN_LO_F)  DELAY(1000)  XFOFF(VOSSLOH, VOSSLOH_HORN_LO_F)
  DONE
ALIAS(HORN_VOSSLOH_HI_TWICE)
SEQUENCE(HORN_VOSSLOH_HI_TWICE)
  XFON(VOSSLOH, VOSSLOH_HORN_HI_F)  DELAY(250)  XFOFF(VOSSLOH, VOSSLOH_HORN_HI_F)
  DELAY(300)
  XFON(VOSSLOH, VOSSLOH_HORN_HI_F)  DELAY(250)  XFOFF(VOSSLOH, VOSSLOH_HORN_HI_F)
  RETURN

// ########### MANEUVER SEQUENCES ###########
ALIAS(VOSSLOH_RESET_FUNCTIONS)
SEQUENCE(VOSSLOH_RESET_FUNCTIONS)
  // Train driver comes in
  XFOFF(VOSSLOH, VOSSLOH_INTERIOR_LIGHT_F)
  XFOFF(VOSSLOH, VOSSLOH_LIGHTS_F)
  XFOFF(VOSSLOH, VOSSLOH_BRAKE_F)
  XFOFF(VOSSLOH, VOSSLOH_ENGINE_SOUND_F)
  XFOFF(VOSSLOH, VOSSLOH_SHUNTING_MODE_F)
  RETURN

ALIAS(VOSSLOH_COLD_START)
SEQUENCE(VOSSLOH_COLD_START)
  // Train driver comes in
  FON(VOSSLOH_INTERIOR_LIGHT_F)
  DELAY(1500)
  FON(VOSSLOH_BRAKE_F)
  DELAY(4000)
  // Start the engine
  FON(VOSSLOH_ENGINE_SOUND_F)
  DELAY(5000)
  // Lights on
  FON(VOSSLOH_LIGHTS_F)
  DELAY(2000)
  FOFF(VOSSLOH_INTERIOR_LIGHT_F)
  DELAY(2000)
  RETURN

ALIAS(VOSSLOH_FULL_STOP)
SEQUENCE(VOSSLOH_FULL_STOP)
  FOFF(VOSSLOH_ENGINE_SOUND_F)
  DELAY(5000)
  FON(VOSSLOH_INTERIOR_LIGHT_F)
  DELAY(5000)
  FOFF(VOSSLOH_LIGHTS_F)
  DELAY(5000)
  FOFF(VOSSLOH_INTERIOR_LIGHT_F)
  RETURN

ALIAS(VOSSLOH_PREPARE_TO_MOVE)
SEQUENCE(VOSSLOH_PREPARE_TO_MOVE)
  FOFF(VOSSLOH_BRAKE_F)
  DELAY(4000)
  CALL(HORN_VOSSLOH_HI_SHORT)
  DELAY(2000)
  RETURN

ALIAS(VOSSLOH_ENGAGE_BRAKES)
SEQUENCE(VOSSLOH_ENGAGE_BRAKES)
  FON(VOSSLOH_BRAKE_F)
  DELAY(4000)
  RETURN

ALIAS(VOSSLOH_SET_FORWARD_DIRECTION)
SEQUENCE(VOSSLOH_SET_FORWARD_DIRECTION)
  FWD(0)
  DELAY(2000)
  RETURN

ALIAS(VOSSLOH_SET_REVERSE_DIRECTION)
SEQUENCE(VOSSLOH_SET_REVERSE_DIRECTION)
  REV(0)
  DELAY(2000)
  RETURN

ALIAS(VOSSLOH_COMPRESSOR_AFTER_DELAY)
SEQUENCE(VOSSLOH_COMPRESSOR_AFTER_DELAY)
  DELAY(5000)
  XFON(VOSSLOH, VOSSLOH_COMPRESSOR_F)
  DELAY(4000)
  XFOFF(VOSSLOH, VOSSLOH_COMPRESSOR_F)
  RETURN

ALIAS(CLOSE_TURNOUTS)
SEQUENCE(CLOSE_TURNOUTS)
  CLOSE(TURNOUT_WALL) DELAY(100) CLOSE(TURNOUT_EDGE)
  DELAY(2000)
  RETURN

ALIAS(THROW_TURNOUTS)
SEQUENCE(THROW_TURNOUTS)
  THROW(TURNOUT_WALL) DELAY(100) THROW(TURNOUT_EDGE)
  DELAY(2000)
  RETURN

ALIAS(SBB_CARGO_PREPARE_TO_MOVE)
SEQUENCE(SBB_CARGO_PREPARE_TO_MOVE)
    // Notify that SBB Cargo could go
  SENDLOCO(VOSSLOH, HORN_VOSSLOH_HI_TWICE)
  DELAY(2000)
  RETURN

// ##################################
// ########### AUTOMATIONS ###########
// ##################################

AUTOMATION(301,"Test Route 301")
  CALL(HORN_VOSSLOH_LOW_LONG_AFTER_DELAY)
  DONE

AUTOMATION(300,"Vossloh Route 300")
  CALL(VOSSLOH_RESET_FUNCTIONS)
  SET_TRACK(A, MAIN)
  SET_TRACK(B, MAIN)
  POWERON
  DELAY(3000)
  // STARTUP
  SETLOCO(VOSSLOH)
  CALL(VOSSLOH_COLD_START)
  // Set turnouts
  CALL(THROW_TURNOUTS)
  // Prepare to move
  CALL(VOSSLOH_PREPARE_TO_MOVE)

  // ###### Go till the right edge of the layout
  FWD(40)
  ATTIMEOUT(R_EDGE, 18000) // Use timeout to avoid accidents
  DELAY(700)
  STOP
  DELAY(4000)
  CALL(VOSSLOH_ENGAGE_BRAKES)
  // Set turnouts
  CALL(CLOSE_TURNOUTS)
  CALL(VOSSLOH_SET_REVERSE_DIRECTION)
  CALL(VOSSLOH_PREPARE_TO_MOVE)

  // ###### Go till the left edge of the layout
  REV(48)
  START(HORN_VOSSLOH_LOW_LONG_AFTER_DELAY)
  ATTIMEOUT(L_EDGE, 16000)
  STOP
  DELAY(7000)
  CALL(VOSSLOH_ENGAGE_BRAKES)
  // Change lights direction
  CALL(VOSSLOH_SET_FORWARD_DIRECTION)
  DELAY(3000)
  CALL(SBB_CARGO_PREPARE_TO_MOVE)

  // ###### SBB Cargo goes to the left
  SETLOCO(CARGO)
  FWD(34)
  ATTIMEOUT(L_WALL, 12000)
  DELAY(1100)
  STOP
  DELAY(5000)
  // Set turnouts
  CALL(THROW_TURNOUTS)
  // Change lights direction
  REV(0)
  DELAY(2000)
  CALL(SBB_CARGO_PREPARE_TO_MOVE)
  REV(30)

  START(VOSSLOH_COMPRESSOR_AFTER_DELAY)

  ATTIMEOUT(R_EDGE, 17000)
  // Delay a little bit to move loco further
  DELAY(500)
  STOP
  DELAY(5000)
  // Change lights direction
  FWD(0)
  DELAY(1000)
  CALL(SBB_CARGO_PREPARE_TO_MOVE)
  // Go back to the left
  FWD(30)
  ATTIMEOUT(L_WALL, 17000)
  // Delay a little bit to move loco further
  DELAY(1900)
  STOP
  DELAY(5000)
  CALL(CLOSE_TURNOUTS)
  // Change lights direction
  REV(0)
  DELAY(1000)
  CALL(SBB_CARGO_PREPARE_TO_MOVE)
  // Go to the home
  REV(33)
  ATTIMEOUT(R_WALL, 14000)
  STOP
  DELAY(6000)
  // Change lights direction
  FWD(0)
  DELAY(2000)

  // ###### Switch back to Vossloh
  SETLOCO(VOSSLOH)
  CALL(VOSSLOH_PREPARE_TO_MOVE)
  // Go till the right edge of the layout again
  FWD(48)
  ATTIMEOUT(R_EDGE, 15000)
  STOP
  DELAY(6000)
  CALL(VOSSLOH_ENGAGE_BRAKES)
  // Set turnouts
  CALL(THROW_TURNOUTS)
  // Change lights direction
  CALL(VOSSLOH_SET_REVERSE_DIRECTION)
  CALL(VOSSLOH_PREPARE_TO_MOVE)

  // ###### Go to the home
  REV(40)
  ATTIMEOUT(L_WALL, 20000)
  // Delay a little bit to move loco further
  DELAY(2300)
  STOP
  DELAY(6000)
  CALL(VOSSLOH_ENGAGE_BRAKES)
  CALL(VOSSLOH_SET_FORWARD_DIRECTION)
  DELAY(3000)

  // ###### Full stop
  CALL(VOSSLOH_FULL_STOP)
  DONE

